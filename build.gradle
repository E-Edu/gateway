plugins {
  id 'org.springframework.boot' version '2.2.5.RELEASE'
  id 'io.spring.dependency-management' version '1.0.9.RELEASE'
  id 'java'
  id 'checkstyle'
}

group = 'de.the-morpheus.e-du'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

configurations {
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
  compileOnly {
    extendsFrom annotationProcessor
  }
}

repositories {
  mavenCentral()
}

dependencies {

  /* Utility */
  compile group: 'javax.validation', name: 'validation-api', version: '2.0.1.Final'
  compileOnly 'org.projectlombok:lombok'
  annotationProcessor 'org.projectlombok:lombok'
  compile 'com.jcabi:jcabi-manifests:0.7.5'

  /* GraphQL */
  implementation 'com.graphql-java-kickstart:graphql-spring-boot-starter:7.0.0'
  implementation 'com.graphql-java:graphql-java-tools:5.2.4'
  compile group: 'com.graphql-java-kickstart', name: 'playground-spring-boot-starter', version: '7.0.0'

  /* Spring */
  implementation 'org.springframework.boot:spring-boot-starter'
  developmentOnly 'org.springframework.boot:spring-boot-devtools'

  /* Test */
  testImplementation 'org.springframework.security:spring-security-test'

  testImplementation('org.springframework.boot:spring-boot-starter-test') {
    exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
  }
}

test {
  useJUnitPlatform()
}

def getGitHash = { ->
  def stdout = new ByteArrayOutputStream()
  exec {
    commandLine 'git', 'rev-parse', '--short', 'HEAD'
    standardOutput = stdout
  }
  return stdout.toString().trim()
}

bootJar {
  manifest {
    attributes 'Implementation-Version': project.version.toString()
    attributes 'Implementation-Title': project.name.toString()
    attributes 'Commit-Id': getGitHash()
    attributes 'Build-Time': System.currentTimeMillis()
  }
}